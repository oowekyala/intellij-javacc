{
  parserClass="com.github.oowekyala.ijcc.lang.parser.JavaccParser"
  parserUtilClass="com.github.oowekyala.ijcc.lang.parser.JavaccParserUtil"

  implements="com.github.oowekyala.ijcc.lang.psi.JavaccPsiElement"
  extends="com.github.oowekyala.ijcc.lang.psi.impl.JavaccPsiElementImpl"

  elementTypeHolderClass="com.github.oowekyala.ijcc.lang.JavaccTypes"
  elementTypePrefix="JCC_"
  elementTypeClass="com.github.oowekyala.ijcc.lang.psi.IJavaccElementType"
  tokenTypeClass="com.github.oowekyala.ijcc.lang.psi.IJavaccTokenType"

  psiClassPrefix="Jcc"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.oowekyala.ijcc.lang.psi"
  psiImplPackage="com.github.oowekyala.ijcc.lang.psi.impl"

  psiImplUtilClass="com.github.oowekyala.ijcc.lang.parser.JavaccParserUtil"

  parserImports = [
  'static com.intellij.psi.JavaTokenType.*'
  ]

  tokens = [
        LOOKAHEAD_KEYWORD         = 'LOOKAHEAD'
        IGNORE_CASE_OPTION        = 'IGNORE_CASE'
        PARSER_BEGIN_KEYWORD      = 'PARSER_BEGIN'
        PARSER_END_KEYWORD        = 'PARSER_END'
        JAVACODE_KEYWORD          = 'JAVACODE'
        TOKEN_KEYWORD             = 'TOKEN'
        SPECIAL_TOKEN_KEYWORD     = 'SPECIAL_TOKEN'
        MORE_KEYWORD              = 'MORE'
        SKIP_KEYWORD              = 'SKIP'
        TOKEN_MGR_DECLS_KEYWORD   = 'TOKEN_MGR_DECLS'
        EOF_KEYWORD               = 'EOF'


  		PRIVATE_KEYWORD		= "private"
  		PUBLIC_KEYWORD		= "public"
  		PROTECTED_KEYWORD	= "protected"

  		VOID_KEYWORD	= "void"

  		FALSE_KEYWORD	= "false"
  		TRUE_KEYWORD	= "true"
  		NULL_KEYWORD	= "null"


        POUND       = "#"
        COLON       = ":"

        LT          = "<"
        GT          = ">"

  		LPARENTH	= "("
  		RPARENTH	= ")"
  		LBRACE		= "{"
  		RBRACE		= "}"
        LBRACKET    = "["
        RBRACKET    = "]"

  		SEMICOLON	= ";"
  		COMMA	    = ","
  		ELLIPSIS    = "..."
  		DOT         = "."

        EQ          = "="

  		UNION       = "|"
  		TILDE       = "~"
        ASTERISK    = "*"
        PLUS        = "+"
        MINUS       = "-"
        QUESTION    = "?"

        DOUBLE_LITERAL      = "DOUBLE_LITERAL"
        INTEGER_LITERAL     = "INTEGER_LITERAL"
        STRING_LITERAL      = "STRING_LITERAL"
        FLOAT_LITERAL       = "FLOAT_LITERAL"
        LONG_LITERAL        = "LONG_LITERAL"
        CHARACTER_LITERAL   = "CHARACTER_LITERAL"

        C_STYLE_COMMENT     = "C_STYLE_COMMENT"
        END_OF_LINE_COMMENT = "END_OF_LINE_COMMENT"
        DOC_COMMENT         = "DOC_COMMENT"

        BAD_CHARACTER   = "BAD_CHARACTER"

        ACU_CODE        = "ACU_CODE"
  ]
}

javacc_input 	::=
        javacc_options
		PARSER_BEGIN_KEYWORD "(" IDENTIFIER ")"
		java_compilation_unit // TODO inject Java here
		"PARSER_END" "(" IDENTIFIER ")"
		( production )*
		"EOF" // FIXME ?

javacc_options 	    ::=
    [ "options" "{" ( option_binding )* "}" ]

option_binding 	    ::=
        "LOOKAHEAD" "=" INTEGER_LITERAL ";"
	| 	"CHOICE_AMBIGUITY_CHECK" "=" INTEGER_LITERAL ";"
	| 	"OTHER_AMBIGUITY_CHECK" "=" INTEGER_LITERAL ";"
	| 	"AUTOMATIC_ERROR_RECOVERY" "=" java_boolean_literal ";"
	| 	"STATIC" "=" java_boolean_literal ";"
	| 	"SUPPORT_CLASS_VISIBILITY_PUBLIC" "=" java_boolean_literal ";"
	| 	"DEBUG_PARSER" "=" java_boolean_literal ";"
	| 	"DEBUG_LOOKAHEAD" "=" java_boolean_literal ";"
	| 	"DEBUG_TOKEN_MANAGER" "=" java_boolean_literal ";"
	| 	"ERROR_REPORTING" "=" java_boolean_literal ";"
	| 	"JAVA_UNICODE_ESCAPE" "=" java_boolean_literal ";"
	| 	"UNICODE_INPUT" "=" java_boolean_literal ";"
	| 	"IGNORE_CASE" "=" java_boolean_literal ";"
	| 	"USER_TOKEN_MANAGER" "=" java_boolean_literal ";"
	| 	"USER_CHAR_STREAM" "=" java_boolean_literal ";"
	| 	"BUILD_PARSER" "=" java_boolean_literal ";"
	| 	"BUILD_TOKEN_MANAGER" "=" java_boolean_literal ";"
	| 	"TOKEN_EXTENDS" "=" STRING_LITERAL ";"
	| 	"TOKEN_FACTORY" "=" STRING_LITERAL ";"
	| 	"TOKEN_MANAGER_USES_PARSER" "=" java_boolean_literal ";"
	| 	"SANITY_CHECK" "=" java_boolean_literal ";"
	| 	"FORCE_LA_CHECK" "=" java_boolean_literal ";"
	| 	"COMMON_TOKEN_ACTION" "=" java_boolean_literal ";"
	| 	"CACHE_TOKENS" "=" java_boolean_literal ";"
	| 	"OUTPUT_DIRECTORY" "=" STRING_LITERAL ";"

production 	        ::=
        javacode_production
	| 	regular_expr_production
	| 	bnf_production
	| 	token_manager_decls

javacode_production ::=
        "JAVACODE"
        // TODO inject java here
		java_access_modifier java_return_type IDENTIFIER "(" java_parameter_list ")"
		java_block

bnf_production 	    ::=
        // TODO inject java here
        java_access_modifier java_return_type IDENTIFIER "(" java_parameter_list ")" ":"
		java_block
		"{" expansion_choices "}"

regular_expr_production 	::=
        [ lexical_state_list ]
		regexpr_kind [ "[" "IGNORE_CASE" "]" ] ":"
		"{" regexpr_spec ( "|" regexpr_spec )* "}"

token_manager_decls ::=
        "TOKEN_MGR_DECLS" ":" java_block  // TODO inject java here


lexical_state_list 	::=
        "<" "*" ">"
	| 	"<" IDENTIFIER ( "," IDENTIFIER )* ">"

regexpr_kind 	    ::=
        "TOKEN"
	| 	"SPECIAL_TOKEN"
	| 	"SKIP"
	| 	"MORE"

regexpr_spec 	    ::=
    regular_expression [ java_block ] [ ":" IDENTIFIER ]


expansion_choices 	::=
    expansion ( "|" expansion )*

expansion 	        ::=
    ( expansion_unit )*

expansion_unit 	    ::=
        local_lookahead
	| 	java_block
	| 	"(" expansion_choices ")" [ "+" | "*" | "?" ]
	| 	"[" expansion_choices "]"
	| 	[ java_assignment_lhs "=" ] regular_expression
	| 	[ java_assignment_lhs "=" ] IDENTIFIER "(" java_expression_list ")"


local_lookahead 	::=
    "LOOKAHEAD" "(" [ INTEGER_LITERAL ] [ "," ] [ expansion_choices ] [ "," ] [ "{" java_expression "}" ] ")"

/* Regular expressions */

regular_expression 	::=
        STRING_LITERAL
	| 	"<" [ [ "#" ] IDENTIFIER ":" ] complex_regular_expression_choices ">"
	| 	"<" IDENTIFIER ">"
	| 	"<" "EOF" ">"

complex_regular_expression_choices 	::=
    complex_regular_expression ( "|" complex_regular_expression )*

complex_regular_expression 	        ::=
    ( complex_regular_expression_unit )*

complex_regular_expression_unit 	::=
        STRING_LITERAL
	| 	"<" IDENTIFIER ">"
	| 	character_list
	| 	"(" complex_regular_expression_choices ")" [ "+" | "*" | "?" ]

character_list  ::=
    [ "~" ] "[" [ character_descriptor ( "," character_descriptor )* ] "]"

character_descriptor 	::=
    STRING_LITERAL [ "-" STRING_LITERAL ]


// Some java grammar

private java_boolean_literal     ::=  "true" | "false"

private java_return_type         ::=  "void" | java_type
private java_type                ::=  (PRIMITIVE_TYPE | IDENTIFIER) ("[" "]")* // FIXME generics?


private java_access_modifier     ::=  [ "public" | "protected" | "private" ]

java_expression_list             ::= java_expression [ ("," java_expression)* ]
java_parameter_list              ::= java_formal_parameter [ ("," java_formal_parameter)* ]
java_formal_parameter            ::= java_type ["..."] IDENTIFIER ("[" "]")*

external java_compilation_unit   ::= parseJCompilationUnit // The next token is a big ACU_CODE, you must translate the PsiBuilder
external java_block              ::= parseJBlock
external java_assignment_lhs     ::= parseJAssignmentLhs
external java_expression         ::= parseJExpression
